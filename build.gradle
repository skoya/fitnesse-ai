
/* Plan:
 - Create multi-module repo:
   - fit (deps: common)
   - slim (deps: common, networking)
   - common
   - networking
   - ant
   - fitnesse, the wiki server
 - Move file creation to plugin
*/

buildscript {
    repositories {
      mavenCentral()
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.9.0'
    }
}

plugins {
  id 'java'
  id "maven-publish"
  id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
  id 'signing'
  id "com.github.ben-manes.versions" version "0.53.0"
  id "groovy"
  id "io.miret.etienne.sass" version "1.6.0"
  id "info.solidsoft.pitest" version "1.19.0-rc.2"
  id "com.google.protobuf" version "0.9.4"
}

group = 'org.fitnesse'
version = new Date().format('yyyyMMdd')
ext {
  vertxVersion = '5.0.6'
  grpcVersion = '1.69.0'
  protobufVersion = '3.25.5'
}

println "Building FitNesse v${project.version}..."

repositories {
  mavenCentral()
}

configurations {
  compile {
    transitive = false
  }
  runtime {
    transitive = false
  }
}

sourceSets {
  main {
    java.srcDir 'src'
    resources.srcDir 'src'
    proto.srcDir 'src/fitnesse/mcp/proto'
    output.resourcesDir java.classesDirectory
  }
  test {
    java.srcDir 'test'
  }
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

dependencies {
  implementation "org.htmlparser:htmlparser:2.1"
  implementation "org.htmlparser:htmllexer:2.1"
  implementation "org.apache.velocity:velocity-engine-core:2.4.1"
  implementation "org.apache.commons:commons-lang3:3.20.0"
  implementation "org.slf4j:slf4j-api:2.0.17"
  implementation "org.slf4j:slf4j-jdk14:2.0.17"
  implementation "com.googlecode.java-diff-utils:diffutils:1.3.0"
  implementation "org.apache.commons:commons-text:1.14.0"
  implementation "org.openjdk.nashorn:nashorn-core:15.7"
  implementation "net.sourceforge.plantuml:plantuml:1.2024.8"
  implementation "io.vertx:vertx-core:${vertxVersion}"
  implementation "io.vertx:vertx-web:${vertxVersion}"
  implementation "io.vertx:vertx-web-client:${vertxVersion}"
  implementation "io.vertx:vertx-auth-oauth2:${vertxVersion}"
  implementation "io.vertx:vertx-config:${vertxVersion}"
  implementation "io.vertx:vertx-grpc:${vertxVersion}"
  implementation "io.vertx:vertx-micrometer-metrics:${vertxVersion}"
  implementation "io.micrometer:micrometer-registry-prometheus:1.15.6"
  implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
  protobuf "io.vertx:vertx-grpc-protoc-plugin:${vertxVersion}"

  compileOnly gradleApi()
  compileOnly "org.apache.ant:ant:1.10.15"
  compileOnly "junit:junit:4.13.2"
  testImplementation "javax.annotation:javax.annotation-api:1.3.2"

  testImplementation gradleApi()
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.3"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.3"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"
  // Keep JUnit4/Vintage temporarily while migrating tests; remove once fully on JUnit5
  testImplementation "junit:junit:4.13.2"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.11.3"
  testImplementation "org.mockito:mockito-core:5.15.2"
  testImplementation "org.mockito:mockito-junit-jupiter:5.15.2"
  testImplementation "org.hamcrest:hamcrest-all:1.3"
  testImplementation "net.javacrumbs.json-unit:json-unit:5.1.0"
  testImplementation "io.vertx:vertx-unit:${vertxVersion}"
  testImplementation "io.vertx:vertx-junit5:${vertxVersion}"

}

 protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protobufVersion}"
  }
  plugins {
    vertxGrpc {
      artifact = "io.vertx:vertx-grpc-protoc-plugin:${vertxVersion}"
    }
    grpcJava {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.plugins {
        vertxGrpc { option 'vertx' }
        grpcJava {}
      }
    }
  }
}

task watchTemplates {
    group = 'development'
    description = 'Watch template files and copy them when changed'

    doLast {
        println "Starting template watcher..."
        println "Waiting for changes..."

        def templatesDir = file('src/fitnesse/resources/templates')
        def lastModified = [:]

        // Initialize last modified times
        templatesDir.eachFileRecurse { file ->
            if (file.name.endsWith('.vm')) {
                lastModified[file.path] = file.lastModified()
            }
        }

        // Create a flag file to allow clean shutdown
        def flagFile = file('build/template-watcher.flag')
        flagFile.parentFile.mkdirs()
        flagFile.createNewFile()

        try {
            while (flagFile.exists() && !Thread.currentThread().isInterrupted()) {
                def hasChanges = false

                templatesDir.eachFileRecurse { file ->
                    if (file.name.endsWith('.vm')) {
                        def currentModified = file.lastModified()
                        def previousModified = lastModified[file.path] ?: 0

                        if (currentModified > previousModified) {
                            hasChanges = true
                            lastModified[file.path] = currentModified
                            println "Changed: ${file.name}"
                        }
                    }
                }

                if (hasChanges) {
                    println "Copying templates..."
                    copy {
                    from 'src/fitnesse/resources/templates'
                    into 'build/classes/java/main/fitnesse/resources/templates'
                        include '**/*.vm'
                    }
                    println "Templates copied. Waiting for changes..."
                }

                Thread.sleep(1000)
            }
        } finally {
            flagFile.delete()
            println "Template watcher stopped."
        }
    }
}

task stopTemplateWatcher {
    group = 'development'
    description = 'Stop the template watcher'

    doLast {
        def flagFile = file('build/template-watcher.flag')
        if (flagFile.exists()) {
            flagFile.delete()
            println "Template watcher stop signal sent."
        } else {
            println "Template watcher is not running."
        }
    }
}

task fitNesseVersion {
  def versionFile = new File("${sourceSets.main.output.resourcesDir}/META-INF/FitNesseVersion.txt")
  outputs.file(versionFile)
  doLast {
    versionFile.parentFile.mkdirs()
    versionFile.text = "v${version}"
    println "fitNesseVersion: ${outputs.files.singleFile.text}"
  }
}

compileSass {
  sourceDir = project.file ("src/fitnesse/resources/scss/custom")
  outputDir = project.file ("${sourceSets.main.output.resourcesDir}/fitnesse/resources/bootstrap/css")
}

task createUpdateLists(type: WikiFileListBuilderTask) {
  outputDirectory = "${sourceSets.main.output.resourcesDir}/Resources"

  files = {
    // Make sure only files in version control are added to the default wiki contents
    "git ls-files FitNesseRoot".execute().text.readLines()
  }

  doNotReplaceFiles = [
    "FitNesseRoot/content.txt",
    "FitNesseRoot/properties.xml",
    "FitNesseRoot/FrontPage/content.txt",
    "FitNesseRoot/FrontPage/properties.xml",
    "FitNesseRoot/PageHeader/content.txt",
    "FitNesseRoot/PageHeader/properties.xml",
    "FitNesseRoot/PlugIns/content.txt",
    "FitNesseRoot/PlugIns/properties.xml",
    "FitNesseRoot/PageFooter/content.txt",
    "FitNesseRoot/PageFooter/properties.xml",
    "FitNesseRoot/TemplateLibrary/content.txt",
    "FitNesseRoot/TemplateLibrary/properties.xml",
    "FitNesseRoot/TemplateLibrary/StaticPage/content.txt",
    "FitNesseRoot/TemplateLibrary/StaticPage/properties.xml",
    "FitNesseRoot/TemplateLibrary/SuitePage/content.txt",
    "FitNesseRoot/TemplateLibrary/SuitePage/properties.xml",
    "FitNesseRoot/TemplateLibrary/TestPage/content.txt",
    "FitNesseRoot/TemplateLibrary/TestPage/properties.xml" ]
}

processResources.dependsOn "fitNesseVersion", "compileSass", "createUpdateLists"

task copyRuntimeLibs(type: Copy) {
  doFirst {
    delete "lib"
  }
  into "lib"
  from configurations.runtimeClasspath
}

test {
  dependsOn copyRuntimeLibs
  maxParallelForks 1
  jvmArgs "-Djava.security.manager=allow"
  // Force Slim to use ephemeral TCP ports instead of legacy pipe (1) to avoid startup failures on newer JVMs.
  systemProperty "slim.port", "0"
  useJUnitPlatform()
}

pitest {
  targetClasses = ['fit.*', 'fitnesse.*']
  pitestVersion = "1.6.4"
  threads = 1 // We can not deal with parallel execution yet
  outputFormats = ['XML', 'HTML']
}

task run(type: JavaExec) {
  dependsOn classes, copyRuntimeLibs
  classpath = sourceSets.main.runtimeClasspath
  mainClass = "fitnesseMain.FitNesseMain"
  systemProperty "slim.port", "0"
  args "-p", "8001", "-e", "0"
}

jar {
  dependsOn createUpdateLists
  into('Resources') {
    from('.') {
      include createUpdateLists.wikiFiles as String[]
    }
  }
  duplicatesStrategy = 'exclude'
  manifest {
    attributes("Main-Class": "fitnesseMain.FitNesseMain",
        "Implementation-Version": archiveVersion)
  }
}

tasks.register('fitnesseAiStandaloneJar', Jar) {
  dependsOn createUpdateLists, classes
  archiveBaseName.set('fitnesse-ai-standalone')
  archiveClassifier.set('')
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from(sourceSets.main.output)
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  manifest {
    attributes("Main-Class": "fitnesseMain.FitNesseMain")
  }
}

task standaloneJar(type: Jar) {
  duplicatesStrategy = 'include'
  dependsOn jar
  archiveBaseName = 'fitnesse'
  archiveClassifier = 'standalone'
  from {
    configurations.runtimeClasspath.collect { zipTree(it) }
  } {
    exclude { FileTreeElement details ->
      details.relativePath.pathString.startsWith('META-INF/') &&
        !details.relativePath.pathString.equals('META-INF/services/org.slf4j.spi.SLF4JServiceProvider')
    }
  }
  from jar.outputs.files.collect {
    zipTree(it)
  }
  manifest {
    attributes("Main-Class": "fitnesseMain.FitNesseMain",
        "Implementation-Version": archiveVersion)
  }
}

task slimJar(type: Jar) {
  dependsOn jar
  archiveBaseName = 'fitnesse'
  archiveClassifier = 'slim'
  from { jar.outputs.files.collect { zipTree(it) } }
    {
    include 'fitnesse/html/*.class'
    include 'fitnesse/slim/**/*.class'
    include 'fitnesse/socketservice/*.class'
    include 'util/*.class'
    include 'fitnesse/util/StringUtils.class'
  }
  manifest {
    attributes("Implementation-Version": archiveVersion)
  }
}

task acceptanceTest(type: JavaExec) {
  mustRunAfter test
  onlyIf { test.didWork }
  classpath = standaloneJar.outputs.files
  mainClass = "fitnesseMain.FitNesseMain"
  javaLauncher = javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(21)
  }
  systemProperty "slim.port", "0"
  args "-o", "-c", "FitNesse.SuiteAcceptanceTests?suite&format=text"
}

check.dependsOn acceptanceTest

task javadocJar(type: Jar) {
  mustRunAfter check
  archiveClassifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  mustRunAfter check
  archiveClassifier = 'sources'
  duplicatesStrategy = 'include'
  from sourceSets.main.allSource
}

task releaseTag(type: Exec) {
  commandLine 'git', 'tag', project.version
  doLast {
    println "Tagged release ${project.version}"
  }
}

task publishTag(type: Exec) {
  commandLine 'git', 'push', '--tags'
  shouldRunAfter releaseTag
}

task release {
  dependsOn 'releaseTag', 'signFitNesseReleasePublication', 'publish', 'publishToSonatype', 'closeAndReleaseSonatypeStagingRepository', 'publishTag'
}

clean {
  delete "lib"
}

publishing {
  publications {
    FitNesseRelease(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      artifact standaloneJar
      artifact slimJar
      groupId 'org.fitnesse'
      artifactId 'fitnesse'
      pom.withXml {
        asNode().get('version') + {
          resolveStrategy = Closure.DELEGATE_FIRST
          name('FitNesse')
          description('The fully integrated standalone wiki, and acceptance testing framework.')
          url('http://fitnesse.org')
          packaging('jar')
        }
        asNode().append(pomLicenses())
        asNode().append(pomScm())
        asNode().append(pomDevelopers())
      }
    }
  }
}

signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  if (signingKey != null && !signingKey.toString().isEmpty()) {
    useInMemoryPgpKeys(signingKey.toString(), signingPassword?.toString())
  }
  sign publishing.publications
}


nexusPublishing {
  repositories {
    // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
    sonatype {
      nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
      snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
    }
  }
}

wrapper {
  gradleVersion = '8.13'
}


def pomLicenses() {
  new NodeBuilder().licenses {
    license {
      name 'Common Public License version 1.0'
      url 'http://www.opensource.org/licenses/cpl1.0'
      distribution 'repo'
    }
  }
}

def pomScm() {
  new NodeBuilder().scm {
    connection 'scm:git:git://github.com/unclebob/fitnesse.git'
    developerConnection 'scm:git:git@github.com:unclebob/fitnesse.git'
    url 'scm:git:http://github.com/unclebob/fitnesse'
  }
}

def pomDevelopers() {
  new NodeBuilder().developers {
    developer {
      id 'unclebob'
      name 'Robert C. Martin'
      email 'unclebob@cleancoder.com'
    }
  }
}
